buildscript {
    ext.kotlin_version = '1.1.3'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

group 'de.bund.bfr'
version '0.0.0a4'

repositories {
    maven { url "http://dl.bintray.com/silebat/RAKIP" }
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.8.7"
    compile 'com.googlecode.ez-vcard:ez-vcard:0.10.2'
    compile 'com.gmail.gcolaianni5:jris:1.0.0'
}

bintray {
    user = 'XXX'
    key = 'XXX'
    publications = ['MyPublication']
    publish = true

    pkg {
        repo = 'RAKIP'
        name = 'rakip_models'
        userOrg = 'silebat'

        version {
            name = project.version
            released = new Date()
            vcsTag = "v${project.version}"
        }
    }
}

task getDeps(type: Copy) {
    from sourceSets.main.runtimeClasspath
    into 'runtime/'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            artifactId project.bintray.pkg.name
            from components.java

            artifact sourcesJar {
                classifier = 'sources'
            }
            artifact javadocJar {
                classifier = 'javadoc'
            }
        }
    }
}